!*
 * Копирайт © 2025 ООО «РУС.ЯЗ»
 * SPDX-License-Identifier: GPLv3+ ИЛИ прориетарная
 *!

функция шаг(значение)
  если значение /\ 0 то
    вернуть 1
  иначе
    вернуть 0
  ---
---

вес_1 = 0
вес_2 = 0
порог = 0
степень_обучения = 0,1

данные = %(
  %(%(0 0) 0)
  %(%(0 1) 0)
  %(%(1 0) 0)
  %(%(1 1) 1)
)

для эпоха от 0 до 10
  для очередь от 0 до 4
    пара = данные.очередь
    входные_данные = пара.0
    ошибка = пара.1 - шаг(вес_1 * входные_данные.0 + вес_2 * входные_данные.1 + порог)

    корректировка = степень_обучения * ошибка
    вес_1 += корректировка * входные_данные.0
    вес_2 += корректировка * входные_данные.1
    порог += корректировка
  ---
  показать("Эпоха:" эпоха)
---

для очередь от 0 до 4
  входные_данные = данные.очередь.0
  значение = шаг(вес_1 * входные_данные.0 + вес_2 * входные_данные.1 + порог)
  показать(входные_данные "|" значение)
---
